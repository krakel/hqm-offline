buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
		classpath 'org.eclipse.jgit:org.eclipse.jgit:4.0.0.201505050340-m2'
        classpath 'org.ajoberstar:gradle-git:1.3.0'
    }
}
import org.ajoberstar.grgit.*

apply plugin: 'java'

repositories {
	mavenCentral()
}

dependencies {
	testCompile group: 'junit', name: 'junit', version:'4.+'
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
	}
	test {
		java {
			srcDir 'test'
		}
	}
} 

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.repo = Grgit.open(file('.'))

group = "de.doerl.hqm"
archivesBaseName = "hqm_offline"
version = "1.1.104"

jar {
	manifest {
		attributes 	'Main-Class': 'de.doerl.hqm.ui.EditMain', 
					'Git-Revision': repo.head().id
	}
} 

task srcZip(type: Zip) {
    classifier = 'src'
    from sourceSets.main.allSource
}

task distZip(type: Zip, dependsOn: 'srcZip') {
	from jar.outputs.files
	from (project.rootDir) {
		include '*.bat'
		include '*.sh'
		include '*.txt'
		exclude 'gradle*.*'
	}
	from srcZip.archivePath
}

build.dependsOn distZip

